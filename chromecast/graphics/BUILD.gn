# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromecast/chromecast.gni")

source_set("graphics") {
  sources = [
    "cast_screen.cc",
    "cast_screen.h",
  ]

  deps = [
    "//ui/aura",
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]
}

group("libcast_graphics") {
  if (use_default_cast_graphics) {
    public_deps = [
      ":libcast_graphics_default",
    ]
  } else {
    public_deps = [
      "$libcast_graphics_path:libcast_graphics_1.0",
    ]
  }
}

if (use_default_cast_graphics) {
  shared_library("libcast_graphics_default") {
    output_name = "libcast_graphics_1.0"

    sources = [
      "cast_egl_platform_default.cc",
      "graphics_properties_default.cc",
      "osd_plane_default.cc",
    ]

    public_deps = [
      "//chromecast/public",
    ]

    deps = [
      "//base",
    ]
  }
}
