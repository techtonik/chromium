# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromecast_build.gni")

declare_args() {
  # Select platforms automatically. Turn this off for manual control.
  ozone_auto_platforms = true
}

declare_args() {
  # The platform that will be active by default.
  ozone_platform = ""

  # Enable individual platforms.
  ozone_platform_caca = false
  ozone_platform_cast = false
  ozone_platform_egltest = false
  ozone_platform_gbm = false
  ozone_platform_ozonex = false
  ozone_platform_test = false

  if (ozone_auto_platforms) {
    if (is_chromecast) {
      # The default platform used at runtime is "cast".
      ozone_platform = "cast"

      # For desktop Chromecast builds, override the default "cast" platform with
      # --ozone_platform=egltest
      ozone_platform_cast = true
      ozone_platform_egltest = true
      ozone_platform_ozonex = true
      ozone_platform_test = true
    } else {
      # Use test as the default platform.
      ozone_platform = "test"

      # Build all platforms whose deps are in install-build-deps.sh.
      # Only these platforms will be compile tested by buildbots.
      ozone_platform_gbm = true
      ozone_platform_test = true
      ozone_platform_egltest = true
    }
  }
}
