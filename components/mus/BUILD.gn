# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")
import("//mojo/public/mojo_application.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")

  source_set("mus") {
    sources = [
      "android_loader.cc",
      "android_loader.h",
    ]

    deps = [
      ":lib",
      "public/interfaces",
      "//mojo/application/public/cpp:sources",
      "//mojo/services/tracing/public/interfaces",
      "//mojo/shell",
      "//third_party/mojo/src/mojo/public/cpp/bindings",
    ]
  }
} else {
  mojo_native_application("mus") {
    sources = [
      "main.cc",
    ]

    deps = [
      ":lib",
      "//mojo/application/public/cpp:sources",
    ]
  }
}

source_set("lib") {
  sources = [
    "mus_app.cc",
    "mus_app.h",
  ]

  deps = [
    "//base",
    "//cc",
    "//cc/surfaces",
    "//components/mus/gles2",
    "//components/mus/public/cpp:common",
    "//components/mus/public/interfaces",
    "//components/mus/surfaces",
    "//components/mus/vm:lib",
    "//mojo/application/public/cpp",
    "//mojo/common:common_base",
    "//mojo/common:tracing_impl",
    "//ui/events",
    "//ui/gl:gl",
    "//ui/platform_window:platform_impls",
    "//ui/platform_window:platform_window",
  ]
}
